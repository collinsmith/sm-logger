#if defined _severity_t_included
    #endinput
#endif
#define _severity_t_included

#include <string>

enum Severity {
  /**
   * Constant used to represent invalid Severities
   * 
   * @deprecated
   */
  Severity_Invalid = -1,

  /**
   * Constant used to represent no Severity (i.e., disables logging)
   */
  Severity_None    = 0,

  /**
   * Constant used to represent the debugging Severity
   */
  Severity_Debug   = 1,

  /**
   * Constant used to represent the lowest Severity (i.e., the most verbose)
   */
  Severity_Lowest  = Severity_Debug,

  /**
   * Constant used to represent the informational Severity
   */
  Severity_Info    = 101,

  /**
   * Constant used to represent the warn Severity
   */
  Severity_Warn    = 201,

  /**
   * Constant used to represent the warn Severity
   * 
   * @note duplicate of {@literal #Severity_Warn}
   */
  Severity_Warning = Severity_Warn,

  /**
   * Constant used to represent the error Severity
   */
  Severity_Error   = 301,

  /**
   * Constant used to represent the highest Severity (i.e., the least verbose)
   * 
   * @note this will not disable logging, it will just make only the most
   *       important messages log
   */
  Severity_Highest = Severity_Error
};

methodmap Severity {

  property int Value {
    public get() {
      return view_as<int>(this);
    }
  }
  
  public void GetName(char[] dst, int len) {
    switch (this.Value) {
      case Severity_Invalid: strcopy(dst, len, "INVALID");
      case Severity_None:    strcopy(dst, len, "NONE");
      case Severity_Debug:   strcopy(dst, len, "DEBUG");
      case Severity_Info:    strcopy(dst, len, "INFO");
      case Severity_Warn:    strcopy(dst, len, "WARN");
      case Severity_Error:   strcopy(dst, len, "ERROR");
      default:               IntToString(this.Value, dst, len);
    }
  }

};